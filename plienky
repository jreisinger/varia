#!/usr/bin/env perl
# Check whether Tesco diapers got any cheaper.
use strict;
use warnings;
no warnings qw(numeric);
use v5.10;
use autodie;
use Mojo::UserAgent;
use Data::Dumper;
use FindBin qw($Bin);
use File::Basename;

my @emails = @ARGV or die "Usage: $0 <email address> [<email address 2> ...]\n";

# Diapers IDs we check
my @ids = qw(
  2002016460814
  2002120607270
  2002016460777
  2002120607287
  2002016460784
);

# Get info from web
my @items;
my $ua = Mojo::UserAgent->new;
for my $id (@ids) {
    my $url =
      "http://potravinydomov.itesco.sk/sk-SK/ProductDetail/ProductDetail/$id";
    my $dom = $ua->get( $url => { Accept => '*/*' } )->res->dom;

    my $title;
    my $price;

    for my $e ( $dom->find('title')->each ) {
        my $text = $e->text;
        $text =~ s/^\s*//;
        $text =~ s/\r//;                     # carriage return
        $text =~ s/ - Potraviny - Tesco//;
        chomp $text;
        $title = $text;
    }

    for my $e ( $dom->find('span[class="linePriceAbbr"]')->each ) {
        my $text = $e->text;
        $text =~ s/[()]//g;
        $text =~ s/,/./;
        $price = $text;
    }

    push @items, { price => $price, title => $title, url => $url };
}

# Get last price from file
my $file = "$Bin/." . basename $0;
my $last_price;
if ( -f $file ) {
    open my $fh, '<', $file;
    $last_price = <$fh>;
    close $fh;
}

# Prepare mail body
my $body;
my $price;
for my $item ( sort { $a->{price} cmp $b->{price} } @items ) {
    if ( not $last_price ) {    # first run
        $body = "$item->{title} - $item->{price} ($item->{url})\n";
    } elsif ( $item->{price} < $last_price ) {
        $body = "$item->{title} - $item->{price} ($item->{url})\n";
    }

    $price = $item->{price};
    $price =~ s/ .*$//;
    open my $fh, '>', $file;
    print $fh $price;
    close $fh;

    last;                       # we only check lowest price
}

if ($body) {
    $last_price //= 'n/a';
    send_mail( $_, "Plienky zlacneli $last_price => $price", $body )
      for @emails;
}

sub send_mail {
    my $recipient = shift;
    my $subject   = shift;
    my $body      = shift;

    my $mailexe = '/usr/bin/mail';

    open my $MAIL, "|$mailexe -s '$subject' '$recipient'"
      or die "Can't send mail: $!\n";

    binmode $MAIL, ":encoding(UTF-8)";
    print $MAIL $body;
}
