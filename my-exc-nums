#!/usr/bin/env perl
# http://www.masteringperl.org/2015/05/computing-excellent-numbers/
use strict;
use warnings;
use Time::HiRes qw(gettimeofday tv_interval);

my $num        = 0;                # starting point
my $old_length = 0;
my $time0      = [gettimeofday];

# program interrupted => let's print statistics
$SIG{INT} = sub {
    my $res = $num / tv_interval( $time0, [gettimeofday] );
    printf "Interrupted at $num. Did %.0f numbers / second on average.\n", $res;
    exit 1;
};

while (1) {
    $num++;
    my $length = length $num;

    # new order of magnitude => let's print statistics
    if ( $length > $old_length ) {
        my $res = $num / tv_interval( $time0, [gettimeofday] );
        printf "Working on length %d. Did %.0f numbers / second on average.\n",
          $length, $res;
        $old_length = $length;
    }

    # odd numbers are prepended by 0
    if ( $length % 2 ) {
        $num = '0' . $num;
        $length++;
    }

    my $half  = $length / 2;
    my $front = substr $num, 0, $half;
    my $back  = substr $num, $half, $half;

    if ( $back**2 - $front**2 == $num ) {
        print "$num\n";
    }
}
