#!/usr/bin/perl
# Search for a common error in serverclass.conf
use strict;
use warnings;

my $in_stanza = 0;
my $class;
while (<>)
{
    if (not $in_stanza and /^\s*\[serverClass:([^\]]+)]/)
    {
        $class = $1;
        $in_stanza = 1;
    }

    if ($in_stanza and /^[#\s]+$/)
    {
        $class = "";
        $in_stanza = 0;
    }

    if ($in_stanza and /\:([^\:]+)\:/)
    {
        my $class2 = $1;
        unless ($class eq $class2)
        {
            print "Error in $ARGV on line $.: $_";
            print "    '$class2' should be '$class', I think\n";
        }
    }
}
continue
{
    if (eof) # not eof()!
    {
        close ARGV; # to reset line numbering ($.) on each input file

        $class = "";
        $in_stanza = 0;
    }
}
